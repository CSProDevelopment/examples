PROC GLOBAL


function ProcessPff(string pff_filename)

	// the pff object will automatically update everything to the new version
	pff this_pff;
	this_pff.load(pff_filename);
	this_pff.save(pff_filename);

	write("Processed PFF %s", pff_filename);

end;


function ProcessOtherFile(string other_filename)

	// read all the lines and update the Version= lines
	file this_file;
	list string lines;

	this_file.open(other_filename);
	this_file.read(lines);
	this_file.close();

	list string version_strings = "Version=CSPro ", '"version": "';

	do numeric counter = 1 while counter <= lines.length()

		do numeric version_type = 1 while version_type <= version_strings.length()

			string line = lines(counter);
			numeric version_pos = pos(version_strings(version_type), line);

			if version_pos > 0 then
				string new_version = version_strings(version_type) + diagnostics("version");
				line[version_pos:length(new_version)] = new_version;
				lines(counter) = line;
			endif;

		enddo;

	enddo;

	this_file.open(other_filename, create);
	this_file.write(lines);
	this_file.close();

	write("Processed other file %s", other_filename);

end;


PROC UPDATE_EXAMPLE_FILES_FF

preproc

	if accept("Are you running CSBatch with the filewrite escapes commented out?", "Yes", "No") <> 1 then
		stop(1);
	endif;

	write("");
	write("");
	write("-------------------------------------------------------------------------------------------------------------");
	write("Remember to search for CSPro <old version> in all the files, as there may be non-application file references.");
	write("-------------------------------------------------------------------------------------------------------------");
	write("");
	write("");

	list string files;
	dirlist(files, "..", "*.*", recursive);

	do numeric counter = 1 while counter <= files.length()

		string this_filename = files(counter);

		// skip this application's files
		if pos("Update Example Files", this_filename) > 0 then
			next;
		endif;

		// calculate the extension
		string extension = tolower(path.getExtension(this_filename));

		if extension = ".pff" then
			ProcessPff(this_filename);

		elseif extension in ".anm", ".apc", ".bch", ".csds", ".dcf", ".ent",
							".fmf", ".ord", ".qsf", ".sva",  ".trs", ".xtb", ".xts" then
			ProcessOtherFile(this_filename);

		else
			next;

		endif;

	enddo;
