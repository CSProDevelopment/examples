PROC GLOBAL

// Callback function called when marker is dragged
function onDragStructureMarker(map mp, numeric marker)
	// Save the new location after drag
	ST_LATITUDE = mp.getMarkerLatitude(marker);
	ST_LONGITUDE = mp.getMarkerLongitude(marker);
end;

// Add a marker to map at the location of the structure that the user
// can drag to update the structure location
function setUpMapForStructureMarkerDrag(map mp)
	numeric structureMarker = mp.addMarker(ST_LATITUDE, ST_LONGITUDE);
	mp.setMarkerOnDrag(structureMarker, onDragStructureMarker(mp, structureMarker)); 
	mp.setTitle("Drag the marker to update the location of the structure");
end;

// Callback function called when user taps map
function onClickMapToPlaceStructure(map mp)
	// Save the clicked location
	ST_LATITUDE = mp.getLastClickLatitude();
	ST_LONGITUDE = mp.getLastClickLongitude();
	
	// Allow the user to drag marker to adjust position
	setUpMapForStructureMarkerDrag(mp);
end;

// Set onClick callback for the map to set structure location to 
// tapped location 
function setUpMapForTapToPlaceStructure(map mp)
	mp.setOnClick(onClickMapToPlaceStructure(mp));
	mp.setTitle("Tap the map at the location of the structure");
end;

// Callback function called when the user taps the done button
function onClickMapDone(map mp)
	// Don't let the user quit until they have chosen the location
	if ST_LATITUDE = notappl or ST_LONGITUDE = notappl then
		errmsg("Please tap on the map at the location of the building before contiuing to the next question");
	else
		mp.hide();
	endif;
end;

PROC STRUCTURES_FF

PROC ST_LOCATION_PLACEHOLDER
onfocus

// Show a map to allow user to provide location of the structure
// Location will be saved in the variables ST_LATITUDE and ST_LONGITUDE
map placeStructureMap;

// If there user already specified a location show a marker at that location that the user can drag
// otherwise show the map that the user can tap on to place the structure
if ST_LATITUDE <> notappl and ST_LONGITUDE <> notappl then
	setUpMapForStructureMarkerDrag(placeStructureMap);
else
	setUpMapForTapToPlaceStructure(placeStructureMap);
endif;

// Add close button
placeStructureMap.addImageButton("../Menu/Resources/close.png", onClickMapDone(placeStructureMap));

// Display the map
placeStructureMap.show();

// If after dismissing map user did provide location go back to previous question
// so that user cannot leave the location blank.
if ST_LATITUDE = notappl or ST_LONGITUDE = notappl then
	reenter ST_TYPE;
else
	advance to ST_NAME;
endif;

PROC ST_NUM_MEMBERS
preproc

// Number of members only applies to ordinary households
ask if ST_TYPE = 1; 
