{Application 'GEOMETRY_123' logic file generated by CSPro}
PROC GLOBAL

map myMap;

function hideMap()
	myMap.hide();
end;

function string pickGeoJson()
	list string geoJsonfiles;
	dirlist(geoJsonfiles, pathname(Application), "*geojson");
	
	list string filenames;
	do numeric i = 1 while i <= geoJsonfiles.length()
		filenames.add(path.getfilename(geoJsonfiles(i)));
	enddo;
	
	if filenames.length() = 0  then
		errmsg("No GeoJSON files exist.");
		reenter;
	endif;
	
	numeric pick = filenames.show();
	if pick > 0 then
		pickGeoJson = geoJsonfiles(pick);
	else
		pickGeoJson = "";
	endif;
end;


PROC GEOMETRY_FF

preproc

// add button to map
myMap.addTextButton("Quit", hideMap());

PROC MENU

onfocus

// clear radio button selection
MENU = notappl;

postproc

if MENU = 1 then
	// trace and save the polygon as a GeoJSON file
	myMap.clearGeometry();
	geometry polygon;
	if polygon.tracepolygon(myMap) then
		polygon.save(maketext("%d-%d.geojson", sysdate("yyyymmdd"), systime("hhMMss")));
	endif;
elseif MENU = 2 then
	// select and view a polygon
	string pick = pickGeoJson();
	if pick <> "" then
		geometry polygon;
		numeric isLoaded = polygon.load(pick);
		if isLoaded then
			myMap.addgeometry(polygon);
			myMap.show();
		endif;
	endif;
elseif MENU = 3 then
	// select and calculate the area and perimeter of a polygon
	string pick = pickGeoJson();
	if pick <> "" then
		geometry polygon;
		polygon.load(pick);
		errmsg("Area = %0.1f m^2, Perimeter = %0.1f m", polygon.area(), polygon.perimeter());
	endif;
endif;

reenter;
