{Application 'LISTING' logic file generated by CSPro}
PROC GLOBAL

PROC STRUCTURES_FF

PROC ST_LOCATION_PLACEHOLDER
preproc

// If the GPS coordinates were already captured do not capture them again.
if ST_LATITUDE <> notappl and ST_LONGITUDE <> notappl then
	noinput;
endif;

// Make "capture GPS" the default so operator just has to tap next
// to start GPS capture
$ = 1;

postproc

if ST_LATITUDE = notappl or ST_LONGITUDE = notappl then

	// Capture GPS
	if not gps(open) then
		errmsg("Failed to start GPS reading. This device probably does not have GPS capabilities");
	else

		// Take GPS reading for up to 120 seconds or until 20m accuracy
		if gps(read, 120, 20) = 1 then
			// Copy reading to dictionary variables
			ST_LATITUDE = gps(Latitude);
			ST_LONGITUDE = gps(Longitude);

			errmsg("Coordinates captured successfully");
		else
			errmsg("Failed to read GPS");
		endif;
	endif;
	
	// Finished - close gps
	gps(close);
	
endif;


PROC ST_NAME

// Set the case label to show in listing and on map to structure number
// and name
setcaselabel(STRUCTURES_DICT, maketext("%v : %s", ST_NUMBER, strip(ST_NAME)));

PROC ST_NUM_MEMBERS
preproc

// Number of members only applies to ordinary households
ask if ST_TYPE = 1; 

