{ Purpose:

  [1] How to make a user-defined function;
  [2] How to interact with the keyer during data entry,
	  alerting them to possible errors

  Keywords Used:  function, recode, reenter

  Notes: This is for demonstration purposes only. For surveys, the
		 following would be permissible; however, for large-scale
		 data entry operations such as a census, we do not encourage
		 data checking to be performed during data entry, as it can
		 significantly slow down the keying operation.
}

PROC GLOBAL

numeric num, leap_yr, e, age;

{ this function assumes a census date of March 17, 2012 }

function check_date(d, m, y);

	if not d in 1:31 then
		check_date = 1;		{ bad day }
		exit;
		
	elseif not m in 1:12 then
		check_date = 2;		{ bad month }
		exit;
		
	elseif not y in 1900:2012 then
		check_date = 3;
		exit;
		
	elseif ( y = 2012 and ( m >= 4 or ( m = 3 and d > 17 ) ) ) then
		check_date = 4;
		exit;
		
	endif;

	check_date = 0;  { assume that it's ok }

	recode	m			=> num;
	   		2			=> 28;
	   		4,6,9,11	=> 30;
						=> 31;
	endrecode;
	
	leap_yr = y % 4;   {remainder of y divided by 4}
	
	if y = 1900 then
		leap_yr = 1;
	endif;
	
	if d > num then
		if m = 2 and d = 29 and leap_yr = 0 then
			check_date = 0; { it's ok, calculate age }
		else
			check_date = 5;	{ bad date in general }
		endif;
	endif;

end;


PROC CHECKDATE_FF


PROC P04_YEAR

	e = check_date (p04_day, p04_month, $);

	if e <> 0 then { error in processing }
	
		if e = 1 then
			errmsg ("Invalid day given, please reenter");
			reenter p04_day;

		elseif e = 2 then
			errmsg ("Invalid month given, please reenter");
			reenter p04_month;

		elseif e = 3 then
			errmsg ("invalid (or out of range) year given, please reenter");
			reenter $;

		elseif e = 4 then
			errmsg ("You have entered a date beyond the census date; please reenter");
			reenter p04_day;

		else
			errmsg ("Invalid date given; please reenter");
			reenter p04_day;

		endif;

	endif;


PROC P04_AGE

	{ if we've reached the age variable, then the DOB data should be correct!
	  check to see if the DOB agrees with the stated age }

	age = 2012 - p04_year;
	
	if ( p04_month > 3 ) or ( p04_month = 3 and p04_day > 17 ) then { person hasn't reached their birthday yet }
		age = age - 1;
	endif;
	
	if age <> $ then
		errmsg ("Keyed age (%d) does not agree with DOB calculated age (%d)", $, age);
		reenter $;
	endif;

