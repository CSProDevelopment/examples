{Application 'BUILD_TRACKING_FILES' logic file generated by CSPro }
PROC GLOBAL 
numeric E;

PROC BUILDGEO_FF

PROC BEALOW
if BEALOW <> notappl then
	{ EAStatus record change }
	if BPROV = notappl or BDIST = notappl or BLOC = notappl then
	    errmsg("Can't change EAs:  %d %d %d   %d %d   %s",BPROV,BDIST,BLOC,BEALOW,BEAHI,BNAME);
	else
		EAPROV = BPROV;
		EADIST = BDIST;
		EALOC  = BLOC;
	    if BEAHI = notappl then
	    	EAEA = BEALOW;
	    	if not loadcase(EASTATUS_DICT,EAPROV,EADIST,EALOC,EAEA) then
	    		writecase(EASTATUS_DICT);
	    	endif;
	    else
	    	if BEAHI < BEALOW then
	    	    errmsg("EA High (%d) < EA Low (%d)",BEAHI,BEALOW);
	    	else
	    		do varying E = BEALOW until E > BEAHI 
			    	EAEA = E;
			    	if not loadcase(EASTATUS_DICT,EAPROV,EADIST,EALOC,EAEA) then
			    		writecase(EASTATUS_DICT);
			    	endif;
	    		enddo;
	    	endif;
	    endif;
	endif;
endif;
{ GeoFile record change }
GPROV = BPROV;
GDIST = BDIST;
GLOC  = BLOC;
if loadcase(GEOFILE_DICT,GPROV,GDIST,GLOC) then
	if BNAME <> "" then
		GNAME = BNAME;
		writecase(GEOFILE_DICT);
	endif;
else
	if GPROV = notappl then
	    GLEVEL = 0;
	elseif GDIST = notappl then
	    GLEVEL = 1;
	elseif GLOC = notappl then
	    GLEVEL = 2;
	else 
	    GLEVEL = 3;
	endif;
	if BNAME = "" then
		if GLEVEL = 0 then
			GNAME = "Country";
		elseif GLEVEL = 1 then
		    GNAME = maketext("Province %d",GPROV);
		elseif GLEVEL = 2 then
		    GNAME = maketext("District %d %d",GPROV,GDIST);
		else
		    GNAME = maketext("Locality %d %d %d",GPROV,GDIST,GLOC);
		endif;
	else
	    GNAME = BNAME;
	endif;
	writecase(GEOFILE_DICT);
endif;
