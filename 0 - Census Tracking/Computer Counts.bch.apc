{Application 'COMPUTER_COUNTS' logic file generated by CSPro }
PROC GLOBAL 
numeric LPROV,LDIST,LLOC,LEA;
numeric CHANGE;
numeric TOLHH,TOLPP;
numeric LEACHH,LEACPP,LEACMALE,LEACFEM;
alpha (3) INITIALS;


function postcounts();
    if CHANGE > 0 then
    	if abs((EAMHH - EACHH)/EAMHH) > TOLHH then
    		write("%2d %2d %3d %3d   ERROR - Housing tolerance error:  Manual = %d  Computer = %d",LPROV,LDIST,LLOC,LEA,EAMHH,EACHH);
    		CHANGE = 0;
    	endif;
    	if abs((EAMPP - EACPP)/EAMPP) > TOLPP then
    		write("%2d %2d %3d %3d   ERROR - Persons tolerance error:  Manual = %d  Computer = %d",LPROV,LDIST,LLOC,LEA,EAMPP,EACPP);
    		CHANGE = 0;
    	endif;
    	if abs((EAMMALE - EACMALE)/EAMMALE) > TOLPP then
    		write("%2d %2d %3d %3d   ERROR - Male tolerance error:     Manual = %d  Computer = %d",LPROV,LDIST,LLOC,LEA,EAMMALE,EACMALE);
    		CHANGE = 0;
    	endif;
    	if abs((EAMFEM - EACFEM)/EAMFEM) > TOLPP then
    		write("%2d %2d %3d %3d   ERROR - Female tolerance error:   Manual = %d  Computer = %d",LPROV,LDIST,LLOC,LEA,EAMFEM,EACFEM);
    		CHANGE = 0;
    	endif;
    	if CHANGE = 2 then
    	    if EACHH = LEACHH and EACPP = LEACPP and EACMALE = LEACMALE and EACFEM = LEACFEM then
				write("%2d %2d %3d %3d   NOCHANGE",LPROV,LDIST,LLOC,LEA);
    	    else
				write("%2d %2d %3d %3d   CHANGED",LPROV,LDIST,LLOC,LEA);
				EAP4DATE = edit("99999999",sysdate("YYYYMMDD"));
				EAP4INIT = INITIALS;	    	 
	    		writecase(EASTATUS_DICT);
    	    endif;
    	endif;
		if CHANGE = 1 then
			write("%2d %2d %3d %3d   OK",LPROV,LDIST,LLOC,LEA);
			EAP4DATE = edit("99999999",sysdate("YYYYMMDD"));
			EAP4INIT = INITIALS;	    	 
    		writecase(EASTATUS_DICT);
    	endif;
    	write("");
    endif;
end; 

PROC CENSUS_2000_DICTIONARY_FF
preproc
    INITIALS = sysparm();
	LPROV = notappl;
	LDIST = notappl;
	LLOC  = notappl;
	LEA   = notappl;
	CHANGE = 0;
	TOLHH = 0.03;
	TOLPP = 0.03;
	
	write("                       Computer Counts Report");
	write("                       ----------------------");
	write("");
	write("PV DS LOC  EA   Action");
	write("-- -- --- ---   ------");
	
	
	
postproc
	postcounts();

PROC QUEST
Preproc
	if PROVINCE <> LPROV or DISTRICT <> LDIST or VILLAGE <> LLOC or EA <> LEA then
		postcounts();
	    EAPROV = PROVINCE;
	    EADIST = DISTRICT;
	    EALOC  = VILLAGE;
	    EAEA   = EA;
	    if loadcase(EASTATUS_DICT,EAPROV,EADIST,EALOC,EAEA) then
	    	{Check if Data Entry is Posted}
	    	if EAP3YEAR = notappl then
	    		write("%2d %2d %3d %3d   ERROR - Data entry is not posted",EAPROV,EADIST,EALOC,EAEA);
	    		write("");
	    		CHANGE = 0;
	    	else
		    	{If not overwrite, check is computer counts present}
	    		if EACHH = notappl then
	    		    CHANGE = 1;

	    		else
    		    	write("%2d %2d %3d %3d   WARNING - Computer counts present already",EAPROV,EADIST,EALOC,EAEA);
    		    	CHANGE = 2;
    		    	LEACHH   = EACHH;
    		    	LEACPP   = EACPP;
    		    	LEACMALE = EACMALE;
    		    	LEACFEM  = EACFEM;
	    		endif;
		    	EACHH   = 0;
		    	EACPP   = 0;
		    	EACMALE = 0;
		    	EACFEM  = 0;	    	endif;
	    else
	        write("%2d %2d %3d %3d   ERROR - Ids not in tracking system",EAPROV,EADIST,EALOC,EAEA);
    		write("");
	        CHANGE = 0;
	    endif;
	    LPROV = PROVINCE;
	    LDIST = DISTRICT;
	    LLOC  = VILLAGE;
	    LEA   = EA;
	endif;
	
PROC P03_SEX
	EACPP = EACPP + 1;
	if $ = 1 then
		EACMALE = EACMALE + 1;
	elseif $ = 2 then
	    EACFEM = EACFEM + 1;
	endif;
	
PROC H01_TYPE
	EACHH = EACHH + 1;
