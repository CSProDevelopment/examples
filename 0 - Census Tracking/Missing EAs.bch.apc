{Application 'MISSING_EAS' logic file generated by CSPro }
PROC GLOBAL 
numeric MPR,MDT,MVI;
numeric PR,DT,VI;
alpha (2) PHASE;

PROC EASTATUS_FF
preproc
    PHASE = sysparm()[1:2];
    MPR = tonumber(sysparm()[3:2]);
    MDT = tonumber(sysparm()[5:2]);
    MVI = tonumber(sysparm()[7:3]);

    if PHASE = "P1" then
        write("          Missing EAs Report - Check-in");
        write("          -----------------------------");
    elseif PHASE = "P2" then
        write("          Missing EAs Report - Coding");
        write("          ---------------------------");
    elseif PHASE = "P3" then
        write("          Missing EAs Report - Data Entry");
        write("          -------------------------------");
    endif;
    write("");
    
PROC EASTATUS_QUEST
preproc
    errmsg("%d %d %d",MPR,MDT,MVI);
    if MPR = default or
            (EAPROV = MPR and MDT = default) or
            (EAPROV = MPR and EADIST = MDT and MVI = default) or
            (EAPROV = MPR and EADIST = MDT and EALOC = MVI) then 
	    if EAPROV <> PR then
	        if PR = 0 then
	            write("Popstan");
	        endif;
	        GPROV = EAPROV;
	        GDIST = notappl;
	        GLOC  = notappl;
	        loadcase(GEOFILE_DICT, GPROV, GDIST, GLOC);
	        write("  %s (%d)", strip(GNAME), GPROV);
	        PR = EAPROV;
	        GDIST = EADIST;
	        loadcase(GEOFILE_DICT, GPROV, GDIST, GLOC);
	        write("    %s (%d)", strip(GNAME), GDIST);
	        DT = EADIST;
	        GLOC = EALOC;
	        loadcase(GEOFILE_DICT, GPROV, GDIST, GLOC);
	        write("      Locality %d", GLOC);
	        VI = EALOC;
	    elseif EADIST <> DT then
	        GPROV = EAPROV;
	        GDIST = EADIST;
	        GLOC  = notappl;
	        loadcase(GEOFILE_DICT, GPROV, GDIST, GLOC);
	        write("    %s (%d)", strip(GNAME), GDIST);
	        DT = EADIST;
	        GLOC = EALOC;
	        loadcase(GEOFILE_DICT, GPROV, GDIST, GLOC);
	        write("      Locality %d", GLOC);
	        VI = EALOC;
	    elseif EALOC <> VI then
	        GPROV = EAPROV;
	        GDIST = EADIST;
	        GLOC  = EALOC;
	        loadcase(GEOFILE_DICT, GPROV, GDIST, GLOC);
	        write("      Locality %d", GLOC);
	        VI = EALOC;
	    endif;
	else
	    skip case;
	endif;  
  
    
PROC EAMHH
    if PHASE = "P1" then 
        if EAP1YEAR = notappl then
            write("          Missing EA - %d", EAEA);
        endif;
    elseif PHASE = "P2" then 
        if EAP2YEAR = notappl then
            write("          Missing EA - %d", EAEA);
        endif;
    elseif PHASE = "P3" then 
        if EAP3YEAR = notappl then
            write("          Missing EA - %d", EAEA);
        endif;
    endif;
