{Popstan Census Data Entry}
PROC GLOBAL

numeric x, heads, empty, i;


PROC CEN_DE


PROC FQUEST

	if H01_TYPE = notappl then
		x = errmsg("Housing record is blank");
	endif;


PROC PERSON000

	if totocc(PERSON) > 0 then
		Heads = count(P02_REL = 1);
		if Heads <> 1 then
			x = errmsg("There are %d heads of household.", Heads);
		endif;
		Empty = count(P02_REL = notappl and P03_SEX = notappl and P04_AGE = notappl);
		if Empty > 0 then
			x = errmsg("There are %d empty person records.", Empty);
			do varying i = 1 until P02_REL(i) = notappl
			enddo;
			reenter P02_REL(i);
		endif;
	endif;


PROC P07_BIRTH

	if P04_AGE < 5 then
		skip to next LINE;
	endif;


PROC P11_LITERACY

	if P04_AGE < 10 then
		skip to next LINE;
	endif;


PROC P12_WORKING

	if $ = 1 then
		ECON_ACTIVE = 1;
		skip to P15_OCC;
	else
		ECON_ACTIVE = 2;
	endif;


PROC P13_LOOKING

	if $ = 1 then
		ECON_ACTIVE = 1;
		skip to P15_OCC;
	endif;


PROC P14_WHY_NOT

	if $ = 1 then
		ECON_ACTIVE = 1;
	else
		skip to P18_BORN;
	endif;


PROC P18_BORN

preproc

	if P04_AGE < 12 or P03_SEX <> 2 then
		skip to next LINE;
	endif;


PROC H01_TYPE

	if $ = 6 then
		skip to H13_PERSONS;
	endif;


PROC H06_TENURE

preproc

	if totocc(PERSON000) = 0 then
		$ = 4;
	endif;

postproc

	if $ = 4 then
		if totocc(PERSON000) > 0 then
			x = errmsg("Household tenure is vacant, but there are %d person records.", soccurs(PERSON));
		endif;
		skip to H13_PERSONS;
	else
		if totocc(PERSON000) = 0 then
			x = errmsg("Household tenure is not vacant, but there are no persons records.");
		endif;
	endif;


PROC H13_PERSONS

preproc

	if H06_TENURE = 4 then
		$ = 0;
	endif;

postproc

	if $ <> totocc(PERSON000) then
		x = errmsg("Number of persons is %d, number of person records is %d.", $, totocc(PERSON000));
		reenter;
	endif;

